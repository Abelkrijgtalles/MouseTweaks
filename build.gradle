buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '[6.0,6.2)', changing: true
        classpath 'org.spongepowered:mixingradle:0.7.+'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

version = project.modVersion
group = "yalter.mousetweaks"
base.archivesName = "MouseTweaks-forge-mc${project.forgeMcVersion}"

java.toolchain.languageVersion = JavaLanguageVersion.of(project.modJavaVersion)

sourceSets {
    main {
        java {
            exclude 'yalter/mousetweaks/fabric'
            exclude 'yalter/mousetweaks/ModMenuApiImpl.java'
            exclude 'yalter/mousetweaks/neoforge'
        }

        resources {
            exclude 'fabric.mod.json'
            exclude 'META-INF/neoforge.mods.toml'
        }
    }
}

mixin {
    config 'mousetweaks.mixins.json'
}

minecraft {
    mappings channel: 'official', version: project.forgeMcVersion

    reobf = false

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'eventbus.api.strictRuntimeChecks', 'true'

            mods {
                mousetweaks {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'eventbus.api.strictRuntimeChecks', 'true'

            mods {
                mousetweaks {
                    source sourceSets.main
                }
            }
        }
    }
}

processResources {
    inputs.property "version", project.modVersion
    filesMatching("META-INF/mods.toml") {
        expand([
                "version": project.modVersion,
                "forgeLoaderVersion": project.forgeLoaderVersion,
                "modCredits": project.modCredits,
                "modDescription": project.modDescription,
        ])
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.forgeMcVersion}-${project.forgeVersion}"
    annotationProcessor "net.minecraftforge:eventbus-validator:${project.forgeEventBusValidatorVersion}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

jar {
    manifest.attributes([
            "Specification-Title"     : "Mouse Tweaks",
            "Specification-Vendor"    : "YaLTeR",
            "Specification-Version"   : "${project.version}",
            "Implementation-Title"    : project.name,
            "Implementation-Version"  : "${project.version}",
            "Implementation-Vendor"   : "YaLTeR",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "TweakClass"              : 'org.spongepowered.asm.launch.MixinTweaker',
            "TweakOrder"              : 0,
            "MixinConfigs"            : "mousetweaks.mixins.json"
    ])
}

task srcJar(type: Jar) {
    archiveClassifier.set("src")
    from sourceSets.main.allJava
}

task apiJar(type: Jar) {
    archiveClassifier.set("api")
    from(sourceSets.main.output) {
        include 'yalter/mousetweaks/api/**'
    }
}

artifacts {
    archives srcJar
    archives apiJar
}
